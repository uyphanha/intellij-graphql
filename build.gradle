buildscript {
  repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.31"
    classpath "com.github.hurricup:gradle-grammar-kit-plugin:2017.1.1"
  }
}

plugins {
  id "org.jetbrains.intellij" version "0.4.9"
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.grammarkit'

grammarKit {
  grammarKitRelease = '1.5.2'
}

import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

intellij {
  updateSinceUntilBuild false
  version '2017.1'
  pluginName = 'GraphQL'
  // Uncomment to test against Android Studio
  // intellij.alternativeIdePath = '/Applications/Android Studio.app'
}

sourceSets {
  main.java.srcDirs += 'src/generated/java'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.7"
        }
    }

repositories {
  jcenter()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:1.3.31"
}

task generateGraphQLLexer(type: GenerateLexer) {
  source 'src/main/grammar/GraphQLLexer.flex'
  targetDir 'src/generated/java/com/intellij/lang/graphql'
  targetClass 'SqliteLexer'
  purgeOldFiles = true
}

task generateGraphQLParser(type: GenerateParser) {
  source "src/main/grammar/GraphQL.bnf"
  targetRoot = 'src/generated/java'
  purgeOldFiles = true
  pathToParser = 'com/intellij/lang/graphql/GraphQLParser'
  pathToPsiRoot = 'com/intellij/lang/graphql/psi'
}

compileKotlin.dependsOn generateGraphQLLexer, generateGraphQLParser